plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

jar {
    zip64 = true
}

shadowJar {
    zip64 = true
}

repositories {
    mavenCentral()
    maven { url = "https://www.jetbrains.com/intellij-repository/releases" }
}

configurations {
    ideaIC
}

tasks.register('installDependencies', Sync) {
    dependsOn configurations.ideaIC
    from {
        configurations.ideaIC.collect { zipTree(it) }
    }
    into "$buildDir/idea"
}

dependencies {
    ideaIC group: "com.jetbrains.intellij.idea", name: "ideaIC", version: "2020.2", ext: "zip"
    implementation fileTree(tasks.installDependencies.outputs.files.singleFile) {
        builtBy tasks.installDependencies
        include "lib/**/*.jar"
    }
}
