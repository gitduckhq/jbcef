plugins {
    id 'java'
    id 'de.undercouch.download' // version '4.1.2'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}


static def normalize(String value) {
    return value
        ? value.toLowerCase(Locale.US).replaceAll("[^a-z0-9]+", "")
        : ""
}

def osName = normalize(System.getProperty("os.name"))
def os = osName.startsWith('windows')
        ? 'windows'
        : osName.startsWith('macosx') || osName.startsWith('osx')
        ? 'macosx'
        : 'linux'
def osArch = normalize(System.getProperty("os.arch"))
def arch = osArch.matches('^(x8664|amd64|ia32e|em64t|x64)$')
        ? 'amd64'
        : osArch.matches('^(x8632|x86|i[3-6]86|ia32|x32)$')
        ? 'i386'
        : osArch.matches('^(arm|arm32)$')
        ? 'arm'
        : 'arm64'

tasks.register('prepareBuildDir') {
    mkdir buildDir
    outputs.dir(buildDir)
}

tasks.register('downloadJcef', Download) {
    dependsOn tasks.prepareBuildDir
    src "https://github.com/jcefmaven/jcefbuild/releases/download/1.0.18/${os}-${arch}.tar.gz"
    dest buildDir
    overwrite false
}

tasks.register('decompressJcef', Copy) {
    dependsOn tasks.downloadJcef
    from tarTree(resources.gzip(tasks.downloadJcef.outputs.files.singleFile))
    into "$buildDir/jcef"
}

dependencies {
    implementation fileTree(tasks.decompressJcef.outputs.files.singleFile) {
        builtBy tasks.decompressJcef
        include "**/*.jar"
    }
}
